//MODEL:
//test on Dynare to find the lagrangean multipliers.
//We consider a standard NK model. We use the FOCS of the competitive economy and we aim at calculating the Ramsey optimal problem.

//------------------------------------------------------------------------------------------------------------------------
//1. Variable declaration
//------------------------------------------------------------------------------------------------------------------------

var pie, c, h, r, z;

//4 variables + 1 shock

varexo u;




//------------------------------------------------------------------------------------------------------------------------
// 2. Parameter declaration and calibration
//-------------------------------------------------------------------------------------------------------------------------

parameters beta, rho, chi, theta, phi, tau;

beta=0.99;
chi = 1;          //coefficient associated to labor effort disutility  
theta=5;          //elasticity for each variety of consumption
phi=1;            //price stickyness
tau=1/(theta-1);  //efficient tax

rho=0.9;  //coefficient associated to productivity shock


//-----------------------------------------------------------------------------------------------------------------------
// 3. The model
//-----------------------------------------------------------------------------------------------------------------------


model;
z=rho*z(-1)+u;
1/c=beta*(1/c(+1))*((1+r)/pie(+1));
(tau*(1-theta)+1) +theta*(chi*h*c/exp(z)-1) = phi*(pie-1)*pie - beta*phi*(pie(+1)-1)*pie(+1);
exp(z)*h = c*(1+(phi/2)*(pie-1)^2);
end;

write_latex_dynamic_model;
//--------------------------------------------------------------------------------------------------------------------------
// 4. Steady state
//---------------------------------------------------------------------------------------------------------------------------

initval;
pie=1.1;
r=1/beta;
c=0.9;
h=0.9;
z=0;
end;



//---------------------------------------------------------------------------------------------------------------------------
// 5. shocks
//---------------------------------------------------------------------------------------------------------------------------

shocks;
var u; stderr 0.008;
end;

//--------------------------------------------------------------------------------------------------------------------------
// 6. Ramsey problem
//--------------------------------------------------------------------------------------------------------------------------

planner_objective(log(c)-(chi/2)*h^2);

ramsey_policy(planner_discount=0.99,order=1);



